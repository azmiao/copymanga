plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {
    defaultConfig {
        compileSdk 34
        applicationId 'top.fumiama.copymanga'
        minSdkVersion 23
        //noinspection OldTargetApi
        targetSdkVersion 34
        versionCode 75
        versionName '2.5.4'
        resourceConfigurations += ['zh', 'zh-rCN']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        aaptOptions {
            cruncherEnabled = false
        }
    }

    ndkVersion "26.3.11579264"

    signingConfigs {
        release {
            enableV1Signing true
            enableV2Signing true
            enableV3Signing true
            enableV4Signing true
        }
        debug {
            enableV1Signing true
            enableV2Signing true
            enableV3Signing true
            enableV4Signing true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            vcsInfo.include false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        /*winrelease {r
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }*/
        debug{
            //minifyEnabled true
            //shrinkResources true
            //vcsInfo.include false
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    bundle{
        density{
            enableSplit = true
        }
        language{
            enableSplit = false
        }
    }
    namespace 'top.fumiama.dmzj.copymanga'
}

Properties props = new Properties()
def propFile = file('signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        android.signingConfigs.debug.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.debug.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.debug.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.debug.keyPassword = props['KEY_PASSWORD']
    }
}

dependencies {
    api fileTree(include: ['*.aar'], dir: 'libs') // https://stackoverflow.com/a/63029110/28801553

    //noinspection GradleDependency
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    //noinspection GradleDependency
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.7'
    //noinspection GradleDependency
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.7'
    testImplementation 'junit:junit:4.13.2'
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation 'com.github.yalantis:ucrop:2.2.6'
    implementation 'io.github.zguop:pager2Banner:1.0.5'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    //noinspection KaptUsageInsteadOfKsp
    kapt 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'com.github.vovaksenov99:OverscrollableScrollView:1.0'
    implementation 'com.github.liaoinstan.SpringView:library:0a24d3e9dd'
    implementation 'com.github.zawadz88:MaterialPopupMenu:4.1.0'
    implementation files('libs/com.lapism/search-2.4.1.aar') // https://stackoverflow.com/a/63029110/28801553
    //noinspection GradleDependency
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'com.airbnb.android:lottie:6.6.6'
    implementation 'net.java.dev.jna:jna:5.17.0@aar'
}
